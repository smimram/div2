<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Cubical.Core.Primitives</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">{-

This file document and export the main primitives of Cubical Agda. It
also defines some basic derived operations (composition and filling).

-}</a>
<a id="149" class="Symbol">{-#</a> <a id="153" class="Keyword">OPTIONS</a> <a id="161" class="Pragma">--safe</a> <a id="168" class="Symbol">#-}</a>
<a id="172" class="Keyword">module</a> <a id="179" href="Cubical.Core.Primitives.html" class="Module">Cubical.Core.Primitives</a> <a id="203" class="Keyword">where</a>

<a id="210" class="Keyword">open</a> <a id="215" class="Keyword">import</a> <a id="222" href="Agda.Builtin.Cubical.Path.html" class="Module">Agda.Builtin.Cubical.Path</a> <a id="248" class="Keyword">public</a>
<a id="255" class="Keyword">open</a> <a id="260" class="Keyword">import</a> <a id="267" href="Agda.Builtin.Cubical.Sub.html" class="Module">Agda.Builtin.Cubical.Sub</a> <a id="292" class="Keyword">public</a>
  <a id="301" class="Keyword">renaming</a> <a id="310" class="Symbol">(</a> <a id="312" href="Agda.Builtin.Cubical.Sub.html#223" class="Postulate">inc</a> <a id="316" class="Symbol">to</a> <a id="319" class="Postulate">inS</a>
           <a id="334" class="Symbol">;</a> <a id="336" href="Agda.Builtin.Cubical.Sub.html#407" class="Primitive">primSubOut</a> <a id="347" class="Symbol">to</a> <a id="350" class="Primitive">outS</a>
           <a id="366" class="Symbol">)</a>
<a id="368" class="Keyword">open</a> <a id="373" class="Keyword">import</a> <a id="380" href="Agda.Primitive.Cubical.html" class="Module">Agda.Primitive.Cubical</a> <a id="403" class="Keyword">public</a>
  <a id="412" class="Keyword">renaming</a> <a id="421" class="Symbol">(</a> <a id="423" href="Agda.Primitive.Cubical.html#350" class="Primitive">primIMin</a>       <a id="438" class="Symbol">to</a> <a id="441" class="Primitive">_∧_</a>  <a id="446" class="Comment">-- I → I → I</a>
           <a id="470" class="Symbol">;</a> <a id="472" href="Agda.Primitive.Cubical.html#375" class="Primitive">primIMax</a>       <a id="487" class="Symbol">to</a> <a id="490" class="Primitive">_∨_</a>  <a id="495" class="Comment">-- I → I → I</a>
           <a id="519" class="Symbol">;</a> <a id="521" href="Agda.Primitive.Cubical.html#400" class="Primitive">primINeg</a>       <a id="536" class="Symbol">to</a> <a id="539" class="Primitive">~_</a>   <a id="544" class="Comment">-- I → I</a>
           <a id="564" class="Symbol">;</a> <a id="566" href="Agda.Primitive.Cubical.html#1132" class="Postulate">isOneEmpty</a>     <a id="581" class="Symbol">to</a> <a id="584" class="Postulate">empty</a>
           <a id="601" class="Symbol">;</a> <a id="603" href="Agda.Primitive.Cubical.html#1654" class="Primitive">primComp</a>       <a id="618" class="Symbol">to</a> <a id="621" class="Primitive">comp</a>
           <a id="637" class="Symbol">;</a> <a id="639" href="Agda.Primitive.Cubical.html#1881" class="Primitive">primHComp</a>      <a id="654" class="Symbol">to</a> <a id="657" class="Primitive">hcomp</a>
           <a id="674" class="Symbol">;</a> <a id="676" href="Agda.Primitive.Cubical.html#1808" class="Primitive">primTransp</a>     <a id="691" class="Symbol">to</a> <a id="694" class="Primitive">transp</a>
           <a id="712" class="Symbol">;</a> <a id="714" href="Agda.Primitive.Cubical.html#486" class="Postulate">itIsOne</a>        <a id="729" class="Symbol">to</a> <a id="732" class="Postulate">1=1</a> <a id="736" class="Symbol">)</a>

<a id="739" class="Comment">-- These two are to make sure all the primitives are loaded and ready</a>
<a id="809" class="Comment">-- to compute hcomp/transp for the universe.</a>
<a id="854" class="Keyword">import</a> <a id="861" href="Agda.Builtin.Cubical.Glue.html" class="Module">Agda.Builtin.Cubical.Glue</a>
<a id="887" class="Comment">-- HCompU is already imported from Glue, and older Agda versions do</a>
<a id="955" class="Comment">-- not have it. So we comment it out for now.</a>
<a id="1001" class="Comment">-- import Agda.Builtin.Cubical.HCompU</a>

<a id="1040" class="Keyword">open</a> <a id="1045" class="Keyword">import</a> <a id="1052" href="Agda.Primitive.html" class="Module">Agda.Primitive</a> <a id="1067" class="Keyword">public</a>
  <a id="1076" class="Keyword">using</a>    <a id="1085" class="Symbol">(</a> <a id="1087" href="Agda.Primitive.html#597" class="Postulate">Level</a>
           <a id="1104" class="Symbol">;</a> <a id="1106" href="Agda.Primitive.html#417" class="Primitive">SSet</a> <a id="1111" class="Symbol">)</a>
  <a id="1115" class="Keyword">renaming</a> <a id="1124" class="Symbol">(</a> <a id="1126" href="Agda.Primitive.html#764" class="Primitive">lzero</a> <a id="1132" class="Symbol">to</a> <a id="1135" class="Primitive">ℓ-zero</a>
           <a id="1153" class="Symbol">;</a> <a id="1155" href="Agda.Primitive.html#780" class="Primitive">lsuc</a>  <a id="1161" class="Symbol">to</a> <a id="1164" class="Primitive">ℓ-suc</a>
           <a id="1181" class="Symbol">;</a> <a id="1183" href="Agda.Primitive.html#810" class="Primitive Operator">_⊔_</a>   <a id="1189" class="Symbol">to</a> <a id="1192" class="Primitive Operator">ℓ-max</a>
           <a id="1209" class="Symbol">;</a> <a id="1211" href="Agda.Primitive.html#326" class="Primitive">Set</a>   <a id="1217" class="Symbol">to</a> <a id="1220" class="Primitive">Type</a>
           <a id="1236" class="Symbol">;</a> <a id="1238" href="Agda.Primitive.html#381" class="Primitive">Setω</a>  <a id="1244" class="Symbol">to</a> <a id="1247" class="Primitive">Typeω</a> <a id="1253" class="Symbol">)</a>
<a id="1255" class="Keyword">open</a> <a id="1260" class="Keyword">import</a> <a id="1267" href="Agda.Builtin.Sigma.html" class="Module">Agda.Builtin.Sigma</a> <a id="1286" class="Keyword">public</a>

<a id="1294" class="Comment">-- This file document the Cubical Agda primitives. The primitives</a>
<a id="1360" class="Comment">-- themselves are bound by the Agda files imported above.</a>

<a id="1419" class="Comment">-- * The Interval</a>
<a id="1437" class="Comment">-- I : IUniv</a>

<a id="1451" class="Comment">-- Endpoints, Connections, Reversal</a>
<a id="1487" class="Comment">-- i0 i1   : I</a>
<a id="1502" class="Comment">-- _∧_ _∨_ : I → I → I</a>
<a id="1525" class="Comment">-- ~_      : I → I</a>


<a id="1546" class="Comment">-- * Dependent path type. (Path over Path)</a>

<a id="1590" class="Comment">-- Introduced with lambda abstraction and eliminated with application,</a>
<a id="1661" class="Comment">-- just like function types.</a>

<a id="1691" class="Comment">-- PathP : ∀ {ℓ} (A : I → Type ℓ) → A i0 → A i1 → Type ℓ</a>

<a id="1749" class="Keyword">infix</a> <a id="1755" class="Number">4</a> <a id="1757" href="Cubical.Core.Primitives.html#1765" class="Function Operator">_[_≡_]</a>

<a id="_[_≡_]"></a><a id="1765" href="Cubical.Core.Primitives.html#1765" class="Function Operator">_[_≡_]</a> <a id="1772" class="Symbol">:</a> <a id="1774" class="Symbol">∀</a> <a id="1776" class="Symbol">{</a><a id="1777" href="Cubical.Core.Primitives.html#1777" class="Bound">ℓ</a><a id="1778" class="Symbol">}</a> <a id="1780" class="Symbol">(</a><a id="1781" href="Cubical.Core.Primitives.html#1781" class="Bound">A</a> <a id="1783" class="Symbol">:</a> <a id="1785" href="Agda.Primitive.Cubical.html#108" class="Datatype">I</a> <a id="1787" class="Symbol">→</a> <a id="1789" href="Cubical.Core.Primitives.html#1220" class="Primitive">Type</a> <a id="1794" href="Cubical.Core.Primitives.html#1777" class="Bound">ℓ</a><a id="1795" class="Symbol">)</a> <a id="1797" class="Symbol">→</a> <a id="1799" href="Cubical.Core.Primitives.html#1781" class="Bound">A</a> <a id="1801" href="Agda.Primitive.Cubical.html#150" class="InductiveConstructor">i0</a> <a id="1804" class="Symbol">→</a> <a id="1806" href="Cubical.Core.Primitives.html#1781" class="Bound">A</a> <a id="1808" href="Agda.Primitive.Cubical.html#178" class="InductiveConstructor">i1</a> <a id="1811" class="Symbol">→</a> <a id="1813" href="Cubical.Core.Primitives.html#1220" class="Primitive">Type</a> <a id="1818" href="Cubical.Core.Primitives.html#1777" class="Bound">ℓ</a>
<a id="1820" href="Cubical.Core.Primitives.html#1765" class="Function Operator">_[_≡_]</a> <a id="1827" class="Symbol">=</a> <a id="1829" href="Agda.Builtin.Cubical.Path.html#197" class="Postulate">PathP</a>


<a id="1837" class="Comment">-- Non dependent path types</a>

<a id="Path"></a><a id="1866" href="Cubical.Core.Primitives.html#1866" class="Function">Path</a> <a id="1871" class="Symbol">:</a> <a id="1873" class="Symbol">∀</a> <a id="1875" class="Symbol">{</a><a id="1876" href="Cubical.Core.Primitives.html#1876" class="Bound">ℓ</a><a id="1877" class="Symbol">}</a> <a id="1879" class="Symbol">(</a><a id="1880" href="Cubical.Core.Primitives.html#1880" class="Bound">A</a> <a id="1882" class="Symbol">:</a> <a id="1884" href="Cubical.Core.Primitives.html#1220" class="Primitive">Type</a> <a id="1889" href="Cubical.Core.Primitives.html#1876" class="Bound">ℓ</a><a id="1890" class="Symbol">)</a> <a id="1892" class="Symbol">→</a> <a id="1894" href="Cubical.Core.Primitives.html#1880" class="Bound">A</a> <a id="1896" class="Symbol">→</a> <a id="1898" href="Cubical.Core.Primitives.html#1880" class="Bound">A</a> <a id="1900" class="Symbol">→</a> <a id="1902" href="Cubical.Core.Primitives.html#1220" class="Primitive">Type</a> <a id="1907" href="Cubical.Core.Primitives.html#1876" class="Bound">ℓ</a>
<a id="1909" href="Cubical.Core.Primitives.html#1866" class="Function">Path</a> <a id="1914" href="Cubical.Core.Primitives.html#1914" class="Bound">A</a> <a id="1916" href="Cubical.Core.Primitives.html#1916" class="Bound">a</a> <a id="1918" href="Cubical.Core.Primitives.html#1918" class="Bound">b</a> <a id="1920" class="Symbol">=</a> <a id="1922" href="Agda.Builtin.Cubical.Path.html#197" class="Postulate">PathP</a> <a id="1928" class="Symbol">(λ</a> <a id="1931" href="Cubical.Core.Primitives.html#1931" class="Bound">_</a> <a id="1933" class="Symbol">→</a> <a id="1935" href="Cubical.Core.Primitives.html#1914" class="Bound">A</a><a id="1936" class="Symbol">)</a> <a id="1938" href="Cubical.Core.Primitives.html#1916" class="Bound">a</a> <a id="1940" href="Cubical.Core.Primitives.html#1918" class="Bound">b</a>

<a id="1943" class="Comment">-- PathP (λ i → A) x y gets printed as x ≡ y when A does not mention i.</a>
<a id="2015" class="Comment">--  _≡_ : ∀ {ℓ} {A : Type ℓ} → A → A → Type ℓ</a>
<a id="2061" class="Comment">--  _≡_ {A = A} = PathP (λ _ → A)</a>


<a id="2097" class="Comment">-- * @IsOne r@ represents the constraint &quot;r = i1&quot;.</a>
<a id="2148" class="Comment">-- Often we will use &quot;φ&quot; for elements of I, when we intend to use them</a>
<a id="2219" class="Comment">-- with IsOne (or Partial[P]).</a>
<a id="2250" class="Comment">-- IsOne : I → SSet ℓ-zero</a>

<a id="2278" class="Comment">-- i1 is indeed equal to i1.</a>
<a id="2307" class="Comment">-- 1=1 : IsOne i1</a>


<a id="2327" class="Comment">-- * Types of partial elements, and their dependent version.</a>

<a id="2389" class="Comment">-- &quot;Partial φ A&quot; is a special version of &quot;IsOne φ → A&quot; with a more</a>
<a id="2456" class="Comment">-- extensional judgmental equality.</a>
<a id="2492" class="Comment">-- &quot;PartialP φ A&quot; allows &quot;A&quot; to be defined only on &quot;φ&quot;.</a>

<a id="2549" class="Comment">-- Partial : ∀ {ℓ} → I → Type ℓ → SSet ℓ</a>
<a id="2590" class="Comment">-- PartialP : ∀ {ℓ} → (φ : I) → Partial φ (Type ℓ) → SSet ℓ</a>

<a id="2651" class="Comment">-- Partial elements are introduced by pattern matching with (r = i0)</a>
<a id="2720" class="Comment">-- or (r = i1) constraints, like so:</a>

<a id="2758" class="Keyword">private</a>
  <a id="sys"></a><a id="2768" href="Cubical.Core.Primitives.html#2768" class="Function">sys</a> <a id="2772" class="Symbol">:</a> <a id="2774" class="Symbol">∀</a> <a id="2776" href="Cubical.Core.Primitives.html#2776" class="Bound">i</a> <a id="2778" class="Symbol">→</a> <a id="2780" href="Agda.Primitive.Cubical.html#1072" class="Primitive">Partial</a> <a id="2788" class="Symbol">(</a><a id="2789" href="Cubical.Core.Primitives.html#2776" class="Bound">i</a> <a id="2791" href="Cubical.Core.Primitives.html#490" class="Primitive Operator">∨</a> <a id="2793" href="Cubical.Core.Primitives.html#539" class="Primitive Operator">~</a> <a id="2795" href="Cubical.Core.Primitives.html#2776" class="Bound">i</a><a id="2796" class="Symbol">)</a> <a id="2798" href="Cubical.Core.Primitives.html#1220" class="Primitive">Type₁</a>
  <a id="2806" href="Cubical.Core.Primitives.html#2768" class="Function">sys</a> <a id="2810" href="Cubical.Core.Primitives.html#2810" class="Bound">i</a> <a id="2812" class="Symbol">(</a><a id="2813" href="Cubical.Core.Primitives.html#2810" class="Bound">i</a> <a id="2815" class="Symbol">=</a> <a id="2817" href="Agda.Primitive.Cubical.html#150" class="InductiveConstructor">i0</a><a id="2819" class="Symbol">)</a> <a id="2821" class="Symbol">=</a> <a id="2823" href="Cubical.Core.Primitives.html#1220" class="Primitive">Type₀</a>
  <a id="2831" href="Cubical.Core.Primitives.html#2768" class="Function">sys</a> <a id="2835" href="Cubical.Core.Primitives.html#2835" class="Bound">i</a> <a id="2837" class="Symbol">(</a><a id="2838" href="Cubical.Core.Primitives.html#2835" class="Bound">i</a> <a id="2840" class="Symbol">=</a> <a id="2842" href="Agda.Primitive.Cubical.html#178" class="InductiveConstructor">i1</a><a id="2844" class="Symbol">)</a> <a id="2846" class="Symbol">=</a> <a id="2848" href="Cubical.Core.Primitives.html#1220" class="Primitive">Type₀</a> <a id="2854" class="Symbol">→</a> <a id="2856" href="Cubical.Core.Primitives.html#1220" class="Primitive">Type₀</a>

  <a id="2865" class="Comment">-- It also works with pattern matching lambdas:</a>
  <a id="2915" class="Comment">--  http://wiki.portal.chalmers.se/agda/pmwiki.php?n=ReferenceManual.PatternMatchingLambdas</a>
  <a id="sys&#39;"></a><a id="3009" href="Cubical.Core.Primitives.html#3009" class="Function">sys&#39;</a> <a id="3014" class="Symbol">:</a> <a id="3016" class="Symbol">∀</a> <a id="3018" href="Cubical.Core.Primitives.html#3018" class="Bound">i</a> <a id="3020" class="Symbol">→</a> <a id="3022" href="Agda.Primitive.Cubical.html#1072" class="Primitive">Partial</a> <a id="3030" class="Symbol">(</a><a id="3031" href="Cubical.Core.Primitives.html#3018" class="Bound">i</a> <a id="3033" href="Cubical.Core.Primitives.html#490" class="Primitive Operator">∨</a> <a id="3035" href="Cubical.Core.Primitives.html#539" class="Primitive Operator">~</a> <a id="3037" href="Cubical.Core.Primitives.html#3018" class="Bound">i</a><a id="3038" class="Symbol">)</a> <a id="3040" href="Cubical.Core.Primitives.html#1220" class="Primitive">Type₁</a>
  <a id="3048" href="Cubical.Core.Primitives.html#3009" class="Function">sys&#39;</a> <a id="3053" href="Cubical.Core.Primitives.html#3053" class="Bound">i</a> <a id="3055" class="Symbol">=</a> <a id="3057" class="Symbol">λ</a> <a id="3059" class="Symbol">{</a> <a id="3061" class="Symbol">(</a><a id="3062" href="Cubical.Core.Primitives.html#3053" class="Bound">i</a> <a id="3064" class="Symbol">=</a> <a id="3066" href="Agda.Primitive.Cubical.html#150" class="InductiveConstructor">i0</a><a id="3068" class="Symbol">)</a> <a id="3070" class="Symbol">→</a> <a id="3072" href="Cubical.Core.Primitives.html#1220" class="Primitive">Type₀</a>
             <a id="3091" class="Symbol">;</a> <a id="3093" class="Symbol">(</a><a id="3094" href="Cubical.Core.Primitives.html#3053" class="Bound">i</a> <a id="3096" class="Symbol">=</a> <a id="3098" href="Agda.Primitive.Cubical.html#178" class="InductiveConstructor">i1</a><a id="3100" class="Symbol">)</a> <a id="3102" class="Symbol">→</a> <a id="3104" href="Cubical.Core.Primitives.html#1220" class="Primitive">Type₀</a> <a id="3110" class="Symbol">→</a> <a id="3112" href="Cubical.Core.Primitives.html#1220" class="Primitive">Type₀</a>
             <a id="3131" class="Symbol">}</a>

  <a id="3136" class="Comment">-- When the cases overlap they must agree.</a>
  <a id="sys2"></a><a id="3181" href="Cubical.Core.Primitives.html#3181" class="Function">sys2</a> <a id="3186" class="Symbol">:</a> <a id="3188" class="Symbol">∀</a> <a id="3190" href="Cubical.Core.Primitives.html#3190" class="Bound">i</a> <a id="3192" href="Cubical.Core.Primitives.html#3192" class="Bound">j</a> <a id="3194" class="Symbol">→</a> <a id="3196" href="Agda.Primitive.Cubical.html#1072" class="Primitive">Partial</a> <a id="3204" class="Symbol">(</a><a id="3205" href="Cubical.Core.Primitives.html#3190" class="Bound">i</a> <a id="3207" href="Cubical.Core.Primitives.html#490" class="Primitive Operator">∨</a> <a id="3209" class="Symbol">(</a><a id="3210" href="Cubical.Core.Primitives.html#3190" class="Bound">i</a> <a id="3212" href="Cubical.Core.Primitives.html#441" class="Primitive Operator">∧</a> <a id="3214" href="Cubical.Core.Primitives.html#3192" class="Bound">j</a><a id="3215" class="Symbol">))</a> <a id="3218" href="Cubical.Core.Primitives.html#1220" class="Primitive">Type₁</a>
  <a id="3226" href="Cubical.Core.Primitives.html#3181" class="Function">sys2</a> <a id="3231" href="Cubical.Core.Primitives.html#3231" class="Bound">i</a> <a id="3233" href="Cubical.Core.Primitives.html#3233" class="Bound">j</a> <a id="3235" class="Symbol">=</a> <a id="3237" class="Symbol">λ</a> <a id="3239" class="Symbol">{</a> <a id="3241" class="Symbol">(</a><a id="3242" href="Cubical.Core.Primitives.html#3231" class="Bound">i</a> <a id="3244" class="Symbol">=</a> <a id="3246" href="Agda.Primitive.Cubical.html#178" class="InductiveConstructor">i1</a><a id="3248" class="Symbol">)</a>          <a id="3259" class="Symbol">→</a> <a id="3261" href="Cubical.Core.Primitives.html#1220" class="Primitive">Type₀</a>
               <a id="3282" class="Symbol">;</a> <a id="3284" class="Symbol">(</a><a id="3285" href="Cubical.Core.Primitives.html#3231" class="Bound">i</a> <a id="3287" class="Symbol">=</a> <a id="3289" href="Agda.Primitive.Cubical.html#178" class="InductiveConstructor">i1</a><a id="3291" class="Symbol">)</a> <a id="3293" class="Symbol">(</a><a id="3294" href="Cubical.Core.Primitives.html#3233" class="Bound">j</a> <a id="3296" class="Symbol">=</a> <a id="3298" href="Agda.Primitive.Cubical.html#178" class="InductiveConstructor">i1</a><a id="3300" class="Symbol">)</a> <a id="3302" class="Symbol">→</a> <a id="3304" href="Cubical.Core.Primitives.html#1220" class="Primitive">Type₀</a>
               <a id="3325" class="Symbol">}</a>

  <a id="3330" class="Comment">-- (i0 = i1) is actually absurd.</a>
  <a id="sys3"></a><a id="3365" href="Cubical.Core.Primitives.html#3365" class="Function">sys3</a> <a id="3370" class="Symbol">:</a> <a id="3372" href="Agda.Primitive.Cubical.html#1072" class="Primitive">Partial</a> <a id="3380" href="Agda.Primitive.Cubical.html#150" class="InductiveConstructor">i0</a> <a id="3383" href="Cubical.Core.Primitives.html#1220" class="Primitive">Type₁</a>
  <a id="3391" href="Cubical.Core.Primitives.html#3365" class="Function">sys3</a> <a id="3396" class="Symbol">=</a> <a id="3398" class="Symbol">λ</a> <a id="3400" class="Symbol">{</a> <a id="3402" class="Symbol">()</a> <a id="3405" class="Symbol">}</a>


<a id="3409" class="Comment">-- * There are cubical subtypes as in CCHM. Note that these are not</a>
<a id="3477" class="Comment">-- fibrant (hence in SSet ℓ):</a>

<a id="_[_↦_]"></a><a id="3508" href="Cubical.Core.Primitives.html#3508" class="Function Operator">_[_↦_]</a> <a id="3515" class="Symbol">:</a> <a id="3517" class="Symbol">∀</a> <a id="3519" class="Symbol">{</a><a id="3520" href="Cubical.Core.Primitives.html#3520" class="Bound">ℓ</a><a id="3521" class="Symbol">}</a> <a id="3523" class="Symbol">(</a><a id="3524" href="Cubical.Core.Primitives.html#3524" class="Bound">A</a> <a id="3526" class="Symbol">:</a> <a id="3528" href="Cubical.Core.Primitives.html#1220" class="Primitive">Type</a> <a id="3533" href="Cubical.Core.Primitives.html#3520" class="Bound">ℓ</a><a id="3534" class="Symbol">)</a> <a id="3536" class="Symbol">(</a><a id="3537" href="Cubical.Core.Primitives.html#3537" class="Bound">φ</a> <a id="3539" class="Symbol">:</a> <a id="3541" href="Agda.Primitive.Cubical.html#108" class="Datatype">I</a><a id="3542" class="Symbol">)</a> <a id="3544" class="Symbol">(</a><a id="3545" href="Cubical.Core.Primitives.html#3545" class="Bound">u</a> <a id="3547" class="Symbol">:</a> <a id="3549" href="Agda.Primitive.Cubical.html#1072" class="Primitive">Partial</a> <a id="3557" href="Cubical.Core.Primitives.html#3537" class="Bound">φ</a> <a id="3559" href="Cubical.Core.Primitives.html#3524" class="Bound">A</a><a id="3560" class="Symbol">)</a> <a id="3562" class="Symbol">→</a> <a id="3564" href="Agda.Primitive.html#417" class="Primitive">SSet</a> <a id="3569" href="Cubical.Core.Primitives.html#3520" class="Bound">ℓ</a>
<a id="3571" href="Cubical.Core.Primitives.html#3571" class="Bound">A</a> <a id="3573" href="Cubical.Core.Primitives.html#3508" class="Function Operator">[</a> <a id="3575" href="Cubical.Core.Primitives.html#3575" class="Bound">φ</a> <a id="3577" href="Cubical.Core.Primitives.html#3508" class="Function Operator">↦</a> <a id="3579" href="Cubical.Core.Primitives.html#3579" class="Bound">u</a> <a id="3581" href="Cubical.Core.Primitives.html#3508" class="Function Operator">]</a> <a id="3583" class="Symbol">=</a> <a id="3585" href="Agda.Builtin.Cubical.Sub.html#198" class="Postulate">Sub</a> <a id="3589" href="Cubical.Core.Primitives.html#3571" class="Bound">A</a> <a id="3591" href="Cubical.Core.Primitives.html#3575" class="Bound">φ</a> <a id="3593" href="Cubical.Core.Primitives.html#3579" class="Bound">u</a>

<a id="3596" class="Keyword">infix</a> <a id="3602" class="Number">4</a> <a id="3604" href="Cubical.Core.Primitives.html#3508" class="Function Operator">_[_↦_]</a>

<a id="3612" class="Comment">-- Any element u : A can be seen as an element of A [ φ ↦ u ] which</a>
<a id="3680" class="Comment">-- agrees with u on φ:</a>

<a id="3704" class="Comment">-- inS : ∀ {ℓ} {A : Type ℓ} {φ} (u : A) → A [ φ ↦ (λ _ → u) ]</a>

<a id="3767" class="Comment">-- One can also forget that an element agrees with u on φ:</a>

<a id="3827" class="Comment">-- outS : ∀ {ℓ} {A : Type ℓ} {φ : I} {u : Partial φ A} → A [ φ ↦ u ] → A</a>


<a id="3902" class="Comment">-- * Composition operation according to [CCHM 18].</a>
<a id="3953" class="Comment">-- When calling &quot;comp A φ u a&quot; Agda makes sure that &quot;a&quot; agrees with &quot;u i0&quot; on &quot;φ&quot;.</a>
<a id="4036" class="Comment">-- compCCHM : ∀ {ℓ} (A : (i : I) → Type ℓ) (φ : I) (u : ∀ i → Partial φ (A i)) (a : A i0) → A i1</a>

<a id="4134" class="Comment">-- Note: this is not recommended to use, instead use the CHM</a>
<a id="4195" class="Comment">-- primitives! The reason is that these work with HITs and produce</a>
<a id="4262" class="Comment">-- fewer empty systems.</a>


<a id="4288" class="Comment">-- * Generalized transport and homogeneous composition [CHM 18].</a>

<a id="4354" class="Comment">-- When calling &quot;transp A φ a&quot; Agda makes sure that &quot;A&quot; is constant on &quot;φ&quot; (see below).</a>
<a id="4442" class="Comment">-- transp : ∀ {ℓ} (A : I → Type ℓ) (φ : I) (a : A i0) → A i1</a>

<a id="4504" class="Comment">-- &quot;A&quot; being constant on &quot;φ&quot; means that &quot;A&quot; should be a constant function whenever the</a>
<a id="4591" class="Comment">-- constraint &quot;φ = i1&quot; is satisfied. For example:</a>
<a id="4641" class="Comment">-- - If &quot;φ&quot; is &quot;i0&quot; then &quot;A&quot; can be anything, since this condition is vacuously true.</a>
<a id="4727" class="Comment">-- - If &quot;φ&quot; is &quot;i1&quot; then &quot;A&quot; must be a constant function.</a>
<a id="4785" class="Comment">-- - If &quot;φ&quot; is some in-scope variable &quot;i&quot; then &quot;A&quot; only needs to be a constant function</a>
<a id="4873" class="Comment">--   when substituting &quot;i1&quot; for &quot;i&quot;.</a>

<a id="4911" class="Comment">-- When calling &quot;hcomp A φ u a&quot; Agda makes sure that &quot;a&quot; agrees with &quot;u i0&quot; on &quot;φ&quot;.</a>
<a id="4995" class="Comment">-- hcomp : ∀ {ℓ} {A : Type ℓ} {φ : I} (u : I → Partial φ A) (a : A) → A</a>

<a id="5068" class="Keyword">private</a>
  <a id="5078" class="Keyword">variable</a>
    <a id="5091" href="Cubical.Core.Primitives.html#5091" class="Generalizable">ℓ</a>  <a id="5094" class="Symbol">:</a> <a id="5096" href="Agda.Primitive.html#597" class="Postulate">Level</a>
    <a id="5106" href="Cubical.Core.Primitives.html#5106" class="Generalizable">ℓ&#39;</a> <a id="5109" class="Symbol">:</a> <a id="5111" href="Agda.Primitive.Cubical.html#108" class="Datatype">I</a> <a id="5113" class="Symbol">→</a> <a id="5115" href="Agda.Primitive.html#597" class="Postulate">Level</a>

<a id="5122" class="Comment">-- Homogeneous filling</a>
<a id="hfill"></a><a id="5145" href="Cubical.Core.Primitives.html#5145" class="Function">hfill</a> <a id="5151" class="Symbol">:</a> <a id="5153" class="Symbol">{</a><a id="5154" href="Cubical.Core.Primitives.html#5154" class="Bound">A</a> <a id="5156" class="Symbol">:</a> <a id="5158" href="Cubical.Core.Primitives.html#1220" class="Primitive">Type</a> <a id="5163" href="Cubical.Core.Primitives.html#5091" class="Generalizable">ℓ</a><a id="5164" class="Symbol">}</a>
        <a id="5174" class="Symbol">{</a><a id="5175" href="Cubical.Core.Primitives.html#5175" class="Bound">φ</a> <a id="5177" class="Symbol">:</a> <a id="5179" href="Agda.Primitive.Cubical.html#108" class="Datatype">I</a><a id="5180" class="Symbol">}</a>
        <a id="5190" class="Symbol">(</a><a id="5191" href="Cubical.Core.Primitives.html#5191" class="Bound">u</a> <a id="5193" class="Symbol">:</a> <a id="5195" class="Symbol">∀</a> <a id="5197" href="Cubical.Core.Primitives.html#5197" class="Bound">i</a> <a id="5199" class="Symbol">→</a> <a id="5201" href="Agda.Primitive.Cubical.html#1072" class="Primitive">Partial</a> <a id="5209" href="Cubical.Core.Primitives.html#5175" class="Bound">φ</a> <a id="5211" href="Cubical.Core.Primitives.html#5154" class="Bound">A</a><a id="5212" class="Symbol">)</a>
        <a id="5222" class="Symbol">(</a><a id="5223" href="Cubical.Core.Primitives.html#5223" class="Bound">u0</a> <a id="5226" class="Symbol">:</a> <a id="5228" href="Cubical.Core.Primitives.html#5154" class="Bound">A</a> <a id="5230" href="Cubical.Core.Primitives.html#3508" class="Function Operator">[</a> <a id="5232" href="Cubical.Core.Primitives.html#5175" class="Bound">φ</a> <a id="5234" href="Cubical.Core.Primitives.html#3508" class="Function Operator">↦</a> <a id="5236" href="Cubical.Core.Primitives.html#5191" class="Bound">u</a> <a id="5238" href="Agda.Primitive.Cubical.html#150" class="InductiveConstructor">i0</a> <a id="5241" href="Cubical.Core.Primitives.html#3508" class="Function Operator">]</a><a id="5242" class="Symbol">)</a>
        <a id="5252" class="Comment">-----------------------</a>
        <a id="5284" class="Symbol">(</a><a id="5285" href="Cubical.Core.Primitives.html#5285" class="Bound">i</a> <a id="5287" class="Symbol">:</a> <a id="5289" href="Agda.Primitive.Cubical.html#108" class="Datatype">I</a><a id="5290" class="Symbol">)</a> <a id="5292" class="Symbol">→</a> <a id="5294" href="Cubical.Core.Primitives.html#5154" class="Bound">A</a>
<a id="5296" href="Cubical.Core.Primitives.html#5145" class="Function">hfill</a> <a id="5302" class="Symbol">{</a><a id="5303" class="Argument">φ</a> <a id="5305" class="Symbol">=</a> <a id="5307" href="Cubical.Core.Primitives.html#5307" class="Bound">φ</a><a id="5308" class="Symbol">}</a> <a id="5310" href="Cubical.Core.Primitives.html#5310" class="Bound">u</a> <a id="5312" href="Cubical.Core.Primitives.html#5312" class="Bound">u0</a> <a id="5315" href="Cubical.Core.Primitives.html#5315" class="Bound">i</a> <a id="5317" class="Symbol">=</a>
  <a id="5321" href="Cubical.Core.Primitives.html#657" class="Primitive">hcomp</a> <a id="5327" class="Symbol">(λ</a> <a id="5330" href="Cubical.Core.Primitives.html#5330" class="Bound">j</a> <a id="5332" class="Symbol">→</a> <a id="5334" class="Symbol">λ</a> <a id="5336" class="Symbol">{</a> <a id="5338" class="Symbol">(</a><a id="5339" href="Cubical.Core.Primitives.html#5307" class="Bound">φ</a> <a id="5341" class="Symbol">=</a> <a id="5343" href="Agda.Primitive.Cubical.html#178" class="InductiveConstructor">i1</a><a id="5345" class="Symbol">)</a> <a id="5347" class="Symbol">→</a> <a id="5349" href="Cubical.Core.Primitives.html#5310" class="Bound">u</a> <a id="5351" class="Symbol">(</a><a id="5352" href="Cubical.Core.Primitives.html#5315" class="Bound">i</a> <a id="5354" href="Cubical.Core.Primitives.html#441" class="Primitive Operator">∧</a> <a id="5356" href="Cubical.Core.Primitives.html#5330" class="Bound">j</a><a id="5357" class="Symbol">)</a> <a id="5359" href="Cubical.Core.Primitives.html#732" class="Postulate">1=1</a>
                 <a id="5380" class="Symbol">;</a> <a id="5382" class="Symbol">(</a><a id="5383" href="Cubical.Core.Primitives.html#5315" class="Bound">i</a> <a id="5385" class="Symbol">=</a> <a id="5387" href="Agda.Primitive.Cubical.html#150" class="InductiveConstructor">i0</a><a id="5389" class="Symbol">)</a> <a id="5391" class="Symbol">→</a> <a id="5393" href="Cubical.Core.Primitives.html#350" class="Primitive">outS</a> <a id="5398" href="Cubical.Core.Primitives.html#5312" class="Bound">u0</a> <a id="5401" class="Symbol">})</a>
        <a id="5412" class="Symbol">(</a><a id="5413" href="Cubical.Core.Primitives.html#350" class="Primitive">outS</a> <a id="5418" href="Cubical.Core.Primitives.html#5312" class="Bound">u0</a><a id="5420" class="Symbol">)</a>

<a id="5423" class="Comment">-- Heterogeneous composition can defined as in CHM, however we use the</a>
<a id="5494" class="Comment">-- builtin one as it doesn&#39;t require u0 to be a cubical subtype. This</a>
<a id="5564" class="Comment">-- reduces the number of inS&#39;s a lot.</a>
<a id="5602" class="Comment">-- comp : (A : ∀ i → Type (ℓ&#39; i))</a>
<a id="5636" class="Comment">--        {φ : I}</a>
<a id="5654" class="Comment">--        (u : ∀ i → Partial φ (A i))</a>
<a id="5692" class="Comment">--        (u0 : A i0 [ φ ↦ u i0 ])</a>
<a id="5727" class="Comment">--      → ---------------------------</a>
<a id="5765" class="Comment">--        A i1</a>
<a id="5780" class="Comment">-- comp A {φ = φ} u u0 =</a>
<a id="5805" class="Comment">--   hcomp (λ i → λ { (φ = i1) → transp (λ j → A (i ∨ j)) i (u _ 1=1) })</a>
<a id="5878" class="Comment">--         (transp A i0 (outS u0))</a>

<a id="5914" class="Comment">-- Heterogeneous filling defined using comp</a>
<a id="fill"></a><a id="5958" href="Cubical.Core.Primitives.html#5958" class="Function">fill</a> <a id="5963" class="Symbol">:</a> <a id="5965" class="Symbol">(</a><a id="5966" href="Cubical.Core.Primitives.html#5966" class="Bound">A</a> <a id="5968" class="Symbol">:</a> <a id="5970" class="Symbol">∀</a> <a id="5972" href="Cubical.Core.Primitives.html#5972" class="Bound">i</a> <a id="5974" class="Symbol">→</a> <a id="5976" href="Cubical.Core.Primitives.html#1220" class="Primitive">Type</a> <a id="5981" class="Symbol">(</a><a id="5982" href="Cubical.Core.Primitives.html#5106" class="Generalizable">ℓ&#39;</a> <a id="5985" href="Cubical.Core.Primitives.html#5972" class="Bound">i</a><a id="5986" class="Symbol">))</a>
       <a id="5996" class="Symbol">{</a><a id="5997" href="Cubical.Core.Primitives.html#5997" class="Bound">φ</a> <a id="5999" class="Symbol">:</a> <a id="6001" href="Agda.Primitive.Cubical.html#108" class="Datatype">I</a><a id="6002" class="Symbol">}</a>
       <a id="6011" class="Symbol">(</a><a id="6012" href="Cubical.Core.Primitives.html#6012" class="Bound">u</a> <a id="6014" class="Symbol">:</a> <a id="6016" class="Symbol">∀</a> <a id="6018" href="Cubical.Core.Primitives.html#6018" class="Bound">i</a> <a id="6020" class="Symbol">→</a> <a id="6022" href="Agda.Primitive.Cubical.html#1072" class="Primitive">Partial</a> <a id="6030" href="Cubical.Core.Primitives.html#5997" class="Bound">φ</a> <a id="6032" class="Symbol">(</a><a id="6033" href="Cubical.Core.Primitives.html#5966" class="Bound">A</a> <a id="6035" href="Cubical.Core.Primitives.html#6018" class="Bound">i</a><a id="6036" class="Symbol">))</a>
       <a id="6046" class="Symbol">(</a><a id="6047" href="Cubical.Core.Primitives.html#6047" class="Bound">u0</a> <a id="6050" class="Symbol">:</a> <a id="6052" href="Cubical.Core.Primitives.html#5966" class="Bound">A</a> <a id="6054" href="Agda.Primitive.Cubical.html#150" class="InductiveConstructor">i0</a> <a id="6057" href="Cubical.Core.Primitives.html#3508" class="Function Operator">[</a> <a id="6059" href="Cubical.Core.Primitives.html#5997" class="Bound">φ</a> <a id="6061" href="Cubical.Core.Primitives.html#3508" class="Function Operator">↦</a> <a id="6063" href="Cubical.Core.Primitives.html#6012" class="Bound">u</a> <a id="6065" href="Agda.Primitive.Cubical.html#150" class="InductiveConstructor">i0</a> <a id="6068" href="Cubical.Core.Primitives.html#3508" class="Function Operator">]</a><a id="6069" class="Symbol">)</a>
       <a id="6078" class="Comment">---------------------------</a>
       <a id="6113" class="Symbol">(</a><a id="6114" href="Cubical.Core.Primitives.html#6114" class="Bound">i</a> <a id="6116" class="Symbol">:</a> <a id="6118" href="Agda.Primitive.Cubical.html#108" class="Datatype">I</a><a id="6119" class="Symbol">)</a> <a id="6121" class="Symbol">→</a> <a id="6123" href="Cubical.Core.Primitives.html#5966" class="Bound">A</a> <a id="6125" href="Cubical.Core.Primitives.html#6114" class="Bound">i</a>
<a id="6127" href="Cubical.Core.Primitives.html#5958" class="Function">fill</a> <a id="6132" href="Cubical.Core.Primitives.html#6132" class="Bound">A</a> <a id="6134" class="Symbol">{</a><a id="6135" class="Argument">φ</a> <a id="6137" class="Symbol">=</a> <a id="6139" href="Cubical.Core.Primitives.html#6139" class="Bound">φ</a><a id="6140" class="Symbol">}</a> <a id="6142" href="Cubical.Core.Primitives.html#6142" class="Bound">u</a> <a id="6144" href="Cubical.Core.Primitives.html#6144" class="Bound">u0</a> <a id="6147" href="Cubical.Core.Primitives.html#6147" class="Bound">i</a> <a id="6149" class="Symbol">=</a>
  <a id="6153" href="Cubical.Core.Primitives.html#621" class="Primitive">comp</a> <a id="6158" class="Symbol">(λ</a> <a id="6161" href="Cubical.Core.Primitives.html#6161" class="Bound">j</a> <a id="6163" class="Symbol">→</a> <a id="6165" href="Cubical.Core.Primitives.html#6132" class="Bound">A</a> <a id="6167" class="Symbol">(</a><a id="6168" href="Cubical.Core.Primitives.html#6147" class="Bound">i</a> <a id="6170" href="Cubical.Core.Primitives.html#441" class="Primitive Operator">∧</a> <a id="6172" href="Cubical.Core.Primitives.html#6161" class="Bound">j</a><a id="6173" class="Symbol">))</a>
       <a id="6183" class="Symbol">(λ</a> <a id="6186" href="Cubical.Core.Primitives.html#6186" class="Bound">j</a> <a id="6188" class="Symbol">→</a> <a id="6190" class="Symbol">λ</a> <a id="6192" class="Symbol">{</a> <a id="6194" class="Symbol">(</a><a id="6195" href="Cubical.Core.Primitives.html#6139" class="Bound">φ</a> <a id="6197" class="Symbol">=</a> <a id="6199" href="Agda.Primitive.Cubical.html#178" class="InductiveConstructor">i1</a><a id="6201" class="Symbol">)</a> <a id="6203" class="Symbol">→</a> <a id="6205" href="Cubical.Core.Primitives.html#6142" class="Bound">u</a> <a id="6207" class="Symbol">(</a><a id="6208" href="Cubical.Core.Primitives.html#6147" class="Bound">i</a> <a id="6210" href="Cubical.Core.Primitives.html#441" class="Primitive Operator">∧</a> <a id="6212" href="Cubical.Core.Primitives.html#6186" class="Bound">j</a><a id="6213" class="Symbol">)</a> <a id="6215" href="Cubical.Core.Primitives.html#732" class="Postulate">1=1</a>
                <a id="6235" class="Symbol">;</a> <a id="6237" class="Symbol">(</a><a id="6238" href="Cubical.Core.Primitives.html#6147" class="Bound">i</a> <a id="6240" class="Symbol">=</a> <a id="6242" href="Agda.Primitive.Cubical.html#150" class="InductiveConstructor">i0</a><a id="6244" class="Symbol">)</a> <a id="6246" class="Symbol">→</a> <a id="6248" href="Cubical.Core.Primitives.html#350" class="Primitive">outS</a> <a id="6253" href="Cubical.Core.Primitives.html#6144" class="Bound">u0</a> <a id="6256" class="Symbol">})</a>
       <a id="6266" class="Symbol">(</a><a id="6267" href="Cubical.Core.Primitives.html#350" class="Primitive">outS</a> <a id="6272" href="Cubical.Core.Primitives.html#6144" class="Bound">u0</a><a id="6274" class="Symbol">)</a>

<a id="6277" class="Comment">-- Σ-types</a>
<a id="6288" class="Keyword">infix</a> <a id="6294" class="Number">2</a> <a id="6296" href="Cubical.Core.Primitives.html#6306" class="Function">Σ-syntax</a>

<a id="Σ-syntax"></a><a id="6306" href="Cubical.Core.Primitives.html#6306" class="Function">Σ-syntax</a> <a id="6315" class="Symbol">:</a> <a id="6317" class="Symbol">∀</a> <a id="6319" class="Symbol">{</a><a id="6320" href="Cubical.Core.Primitives.html#6320" class="Bound">ℓ</a> <a id="6322" href="Cubical.Core.Primitives.html#6322" class="Bound">ℓ&#39;</a><a id="6324" class="Symbol">}</a> <a id="6326" class="Symbol">(</a><a id="6327" href="Cubical.Core.Primitives.html#6327" class="Bound">A</a> <a id="6329" class="Symbol">:</a> <a id="6331" href="Cubical.Core.Primitives.html#1220" class="Primitive">Type</a> <a id="6336" href="Cubical.Core.Primitives.html#6320" class="Bound">ℓ</a><a id="6337" class="Symbol">)</a> <a id="6339" class="Symbol">(</a><a id="6340" href="Cubical.Core.Primitives.html#6340" class="Bound">B</a> <a id="6342" class="Symbol">:</a> <a id="6344" href="Cubical.Core.Primitives.html#6327" class="Bound">A</a> <a id="6346" class="Symbol">→</a> <a id="6348" href="Cubical.Core.Primitives.html#1220" class="Primitive">Type</a> <a id="6353" href="Cubical.Core.Primitives.html#6322" class="Bound">ℓ&#39;</a><a id="6355" class="Symbol">)</a> <a id="6357" class="Symbol">→</a> <a id="6359" href="Cubical.Core.Primitives.html#1220" class="Primitive">Type</a> <a id="6364" class="Symbol">(</a><a id="6365" href="Cubical.Core.Primitives.html#1192" class="Primitive">ℓ-max</a> <a id="6371" href="Cubical.Core.Primitives.html#6320" class="Bound">ℓ</a> <a id="6373" href="Cubical.Core.Primitives.html#6322" class="Bound">ℓ&#39;</a><a id="6375" class="Symbol">)</a>
<a id="6377" href="Cubical.Core.Primitives.html#6306" class="Function">Σ-syntax</a> <a id="6386" class="Symbol">=</a> <a id="6388" href="Agda.Builtin.Sigma.html#166" class="Record">Σ</a>

<a id="6391" class="Keyword">syntax</a> <a id="6398" href="Cubical.Core.Primitives.html#6306" class="Function">Σ-syntax</a> <a id="6407" class="Bound">A</a> <a id="6409" class="Symbol">(λ</a> <a id="6412" class="Bound">x</a> <a id="6414" class="Symbol">→</a> <a id="6416" class="Bound">B</a><a id="6417" class="Symbol">)</a> <a id="6419" class="Symbol">=</a> <a id="6421" class="Function">Σ[</a> <a id="6424" class="Bound">x</a> <a id="6426" class="Function">∈</a> <a id="6428" class="Bound">A</a> <a id="6430" class="Function">]</a> <a id="6432" class="Bound">B</a>
</pre></body></html>